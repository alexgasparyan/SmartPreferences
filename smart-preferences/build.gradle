apply plugin: 'com.android.library'
apply plugin: 'maven'

def publish_version = '1.0.0'
def localReleaseDest = "${buildDir}/release/${version}"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
    }
}

dependencies {
    api project(':smart-preferences-annotations')
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = 'com.armdroid'
            pom.artifactId = 'smartpreferences'
            pom.version = publish_version
            repository(url: "file://${localReleaseDest}")
        }
    }
}

task zipRelease(type: Zip) {
    from localReleaseDest
    destinationDir buildDir
    archiveName "smartpreferences-${publish_version}.zip"
}

task generateRelease {
    doLast {
        println "Release ${publish_version} can be found at ${localReleaseDest}/"
        println "Release ${publish_version} zipped can be found ${buildDir}/${zipRelease.archiveName}"
    }
}

generateRelease.dependsOn(uploadArchives)
generateRelease.dependsOn(zipRelease)

task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}